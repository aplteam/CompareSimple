 R←Test_052(stopFlag batchFlag);parms;⎕TRAP;tempDir;res;changeFlags;was
⍝ Exercise "These" with two objects, one of which is Linked.  The user is supposed to change the right (non-LINKed) one
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 0 0⍴⍞,0/⎕←'This test needs you: when the comparison tool appears change the right panel. Press <enter> to continue'

 '#.TEMP1' '#.TEMP2'⎕NS¨⊂''

 #.TEMP1.⎕FX' r←Hello' ' r←''World'''
 #.TEMP2.⎕FX' r←Hello' ' r←''Universe''' '⍝ Second version'

 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 res←⎕SE.Link.Create'#.TEMP1'tempDir
 Assert'Linked: #.TEMP1 ←→'{⍺≡(≢⍺)↑⍵}res

 parms←##.CompareSimple.CreateParmsFor_These
 parms.readOnly1←0
 parms.alias1←'Left'
 parms.alias2←'Right'

 was←##.CompareSimple.NoComparison
 ##.CompareSimple.NoComparison←0
 changeFlags←parms ##.CompareSimple.These'#.TEMP1.Hello' '#.TEMP2.Hello'

 →T.GoToTidyUp 0 1≢changeFlags
 →T.GoToTidyUp' r←Hello' ' r←''World'''≢#.TEMP1.⎕NR'Hello'
 →T.GoToTidyUp' r←Hello' ' r←''Universe''' '⍝ Second version'≡#.TEMP2.⎕NR'Hello'

 R←T._OK

∆TidyUp:
 {}⎕SE.Link.Break'#.TEMP1'
 #.⎕EX¨'TEMP1' 'TEMP2'
 ##.CompareSimple.NoComparison←was
 FilesAndDirs.RmDir tempDir
⍝Done
