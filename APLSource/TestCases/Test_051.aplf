 R←Test_051(stopFlag batchFlag);parms;⎕TRAP;tempDir;res;changeFlags;orig;new;was
⍝ Exercise "These" with two objects, one of which is Linked. The user is supposed to change the right (LINKed) one
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :If batchFlag
     R←T._NoBatchTest
     :Return
 :EndIf

 CommTools.Pause'This test needs you: when the comparison tool appears change the right panel. Press <enter> to continue'

 '#.TEMP1' '#.TEMP2'⎕NS¨⊂''

 #.TEMP1.⎕FX' r←Hello' ' r←''World'''
 #.TEMP2.⎕FX' r←Hello' ' r←''Universe''' '⍝ Second version'

 tempDir←FilesAndDirs.GetTempSubDir⊃⎕SI
 res←⎕SE.Link.Create'#.TEMP2'tempDir
 Assert'Linked: #.TEMP2 ←→'{⍺≡(≢⍺)↑⍵}res
 orig←⊃⎕NGET tempDir,'\Hello.aplf'

 parms←##.CompareSimple.CreateParmsFor_These
 parms.readOnly1←0
 parms.alias1←'Left'
 parms.alias2←'Right'

 was←##.CompareSimple.NoComparison
 ##.CompareSimple.NoComparison←0
 changeFlags←parms ##.CompareSimple.These'#.TEMP1.Hello' '#.TEMP2.Hello'
 →T.GoToTidyUp 0 1≢changeFlags
 new←⊃⎕NGET tempDir,'\Hello.aplf'
 →T.GoToTidyUp orig≡new

 R←T._OK

∆TidyUp:
 {}⎕SE.Link.Break'#.TEMP2'
 #.⎕EX¨'TEMP1' 'TEMP2'
 ##.CompareSimple.NoComparison←was
 FilesAndDirs.RmDir tempDir
⍝Done
